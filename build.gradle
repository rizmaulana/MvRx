import com.airbnb.mvrx.AndroidMetaModulePlugin

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.gradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${Versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"

        // Upload with:
        // ./gradlew clean assemble uploadArchives --no-daemon --no-parallel
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.12.0"
    }

    configurations.all {
        resolutionStrategy {
            // Use Proguard 6.2.0
            force "net.sf.proguard:proguard-gradle:6.2.0"
        }
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.9.1")
}

apply plugin: 'io.gitlab.arturbosch.detekt'

plugins.apply(AndroidMetaModulePlugin.class)

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    tasks.whenTaskAdded { task ->
        if (task.name.startsWith("lint")) {
            task.doFirst {
                android.lintOptions.abortOnError true
                android.lintOptions.textReport true
                android.lintOptions.textOutput "stdout"
                android.lintOptions.lintConfig = file("../lint.xml")
            }
        }
    }
}

subprojects {
    apply from: "$rootDir/detekt.gradle"
}
